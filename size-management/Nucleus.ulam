/**
 * Nucleus is a size management element.
 * \color #f29
 * \symbol Nu
 * \symmetries all
 *
 * \author R. Stephens Summerlin
 * \license lgpl
 */
element Nucleus
{
    // Types
    typedef Unsigned(6) ID;
    typedef Unsigned(6) Dist;
    typedef EventWindow.SiteNum SiteNum;
    typedef EventWindow.Symmetry Symmetry;

    // Utilities
    EventWindow ew;

    // Model Parameters
    /** Determines the size */
    parameter Dist maxDist = 16;
    /** Makes nucleus mobile */
    parameter Bool diffuse = false;

    // Data members
    ID id = 1;

    Void die()
    {
        Empty e;
        ew[0] = e;
    }

    Void spread(SiteNum site)
    {
        Cytoplasm c;
        c.id = id;
        c.dist = maxDist;
        ew[site] = c;
    }

    /**
     * Primary runtime logic
     */
    Void behave()
    {
        // Loop through neighbors
        Unsigned(3) memNeig = 0;
        Bool swap = false;
        for(SiteNum site = 1; site <= 4; ++site)
        {
            // If Cytoplasm or empty, excite
            Atom a = ew[site];
            if((a is Cytoplasm) || (a is Empty))
            {
                spread(site);

                // Only swap to site 1, and only if empty or medium
                if(site == 1)
                {
                    swap = true;
                }
            }
            // Count surrounding membranes
            else if(a is Membrane)
            {
                ++memNeig;
            }
            // If Nucleus do nothing
            else if(a is Nucleus)
            {
                //Do Nothing
            }
            // Else do nothing if unknown
            else
            {
                // Do nothing
            }
        }

        // Die if surrounded by membrane
        if(memNeig > 3)
        {
            die();
        }
        // Swap if enabled and is safe
        else if(swap && diffuse)
        {
           ew.swap(0, 1);
        }
        else
        {
            // Do nothing
        }
    }
}

/**
 * Nucleus ID variation enabler
 * \color #2f2
 * \symbol Nu
 * \symmetries normal
 *
 * \author R. Stephens Summerlin
 * \license lgpl
 */
element NucleusB
{
    // Utilities
    EventWindow ew;

    /**
     * Primary runtime logic
     */
    Void behave()
    {
        Nucleus n;
        n.id = 2;
        ew[0] = n;
    }
}

/**
 * Nucleus ID variation enabler
 * \color #ff2
 * \symbol Nu
 * \symmetries normal
 *
 * \author R. Stephens Summerlin
 * \license lgpl
 */
element NucleusC
{
    // Utilities
    EventWindow ew;

    /**
     * Primary runtime logic
     */
    Void behave()
    {
        Nucleus n;
        n.id = 3;
        ew[0] = n;
    }
}
