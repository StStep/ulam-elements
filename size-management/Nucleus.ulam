use Once;

/**
 * Nucleus is a size management element.
 * \color #f29
 * \symbol Nu
 * \symmetries normal
 *
 * \author R. Stephens Summerlin
 * \license lgpl
 */
element Nucleus
{
    // Types
    typedef Unsigned(6) ID;
    typedef Unsigned(4) Inst;
    typedef Unsigned(6) Dist;
    typedef CMap.Key Key;
    typedef CMap.Value Value;
    typedef EventWindow.SiteNum SiteNum;
    typedef EventWindow.Symmetry Symmetry;

    // Utilities
    EventWindow ew;
    Random rand;

    // Model Parameters
    /** Determines the size */
    parameter Dist maxDist = 16;
    /** Determines the chain length */
    parameter Dist chainLen = 16;
    /** Determines the cycles to be stable */
    parameter Unsigned(6) stableMinCycle = 30;

    // Data members
    ID nId = 1; // Nucleus ID, unique per type
    Inst iId = 0; // Instance ID, unique per instance

    Once mOnce;
    Bool stable = false;
    Unsigned(6) stabCount = 0;
    Bool isChain = false;

    /**
     * Hard-coded color options for debugging with
     */
    ARGB getColor(Unsigned selector)
    {
        ColorUtils cu;
        ARGB ret;
        if(stable)
        {
            ret = cu.color(0xFF,0x50,0xFF);
        }
        else // unstable
        {
            ret = cu.color(0xFF,2,0x50);
        }

        return ret;
    }

    Void die()
    {
        Empty e;
        ew[0] = e;
    }

    Void spawnLink(SiteNum site)
    {
        if (nId == 1)
            return;
        Nucleus n;
        n.nId = nId;
        n.iId = (Inst) (iId - 1);
        ew[site] = n;
    }

    Void spread(SiteNum site)
    {
        Cytoplasm c;
        c.nId = nId;
        c.iId = iId;
        c.dist = maxDist;
        c.instMap.empty();
        c.instMap.add((Key) iId, (Value) Value.maxof);
        ew[site] = c;
    }

    Void excite(SiteNum site, Cytoplasm c)
    {
        c.nId = nId;
        c.iId = iId;
        c.dist = maxDist;
        c.instMap.empty();
        c.instMap.add((Key) iId, (Value) Value.maxof);
        ew[site] = c;
    }

    /**
     * Primary runtime logic
     */
    Void behave()
    {
        // Run Once
        if(mOnce.new())
        {
            iId = (Inst) rand.between(0,Inst.maxof);
        }

        // Loop through neighbors
        Unsigned(3) memNeig = 0;
        Dist room[4];
        Bool safe[4];
        Bool seeChild = false;
        Bool seeParent = false;
        SiteNum parentDir = 0;
        Value parentDist = 0;
        for(SiteNum site = 1; site <= 4; ++site)
        {
            // If Empty or Medium, spread
            Atom a = ew[site];
            if((a is Empty) || (a is Medium))
            {
                spread(site);
            }
            // If cytoplasm, excite
            else if(a is Cytoplasm)
            {
                safe[site - 1] = true;
                Cytoplasm c = (Cytoplasm) a;

                // Check for parent
                Value pd = c.instMap.getV((Key)(iId + 1));
                if(parentDist < pd)
                {
                    seeParent = true;
                    parentDist = pd;
                    parentDir = site;
                }

                // Check for child if iId > 1
                if(nId > 1) {
                    Value cd = c.instMap.getV((Key)(iId - 1));
                    if(cd != 0) {
                        seeChild = true;
                    }
                }

                if(site == 1)
                {
                    room[3] = c.room[3];
                }
                else if(site == 2)
                {
                    room[2] = c.room[2];
                }
                else if(site == 3)
                {
                    room[1] = c.room[1];
                }
                // Site == 4
                else
                {
                    room[0] = c.room[0];
                }
                excite(site, c);
            }
            // Count surrounding membranes
            else if(a is Membrane)
            {
                ++memNeig;
            }
            // Die if adjacent nuclei
            else if(a is Nucleus)
            {
                die();
                return;
            }
            // Else die if unknown
            else
            {
               die();
               return;
            }
        }

        // Die if surrounded by membrane or no parent and is link
        if((memNeig > 3) || (isChain && !seeParent))
        {
            die();
            return;
        }

        // Calculate if link
        if(isChain) {
            // Spawn if child not found and iId > 1, opposite dir of parent
            if(!seeChild && iId > 1)
            {
                SiteNum childDir = 0;
                if(parentDir == 1) {
                    childDir = 3;
                }
                else if(parentDir == 2) {
                    childDir = 4;
                }
                else if(parentDir == 3) {
                    childDir = 1;
                }
                else {
                    childDir = 2;
                }
                spawnLink(childDir);
            }

            // Move toward parent if out of range
            Value calcRange = (Value) Value.maxof;
            calcRange = (Value) (calcRange - parentDist);
            if(calcRange > maxDist/2) {
                ew.swap(0, parentDir);
                return;
            }
        }

        // Move to optimize room
        Int udDisp = ((Int) room[2]) - ((Int) room[1]);
        Int lrDisp = ((Int) room[3]) - ((Int) room[0]);

        Dist udAbs;
        if(udDisp < 0)
        {
            udAbs = (Dist) -udDisp;
        }
        else
        {
            udAbs = (Dist) udDisp;
        }

        Dist lrAbs;
        if(lrDisp < 0)
        {
            lrAbs =  (Dist) -lrDisp;
        }
        else
        {
            lrAbs = (Dist) lrDisp;
        }

        Bool isLarge = (udAbs > 2) || (lrAbs > 2);

        // Move left/right
        if(isLarge && (udAbs < lrAbs))
        {
            --stabCount;
            stable = false;
            // Move right
            if(lrDisp > 0)
            {
                if(safe[3])
                {
                    ew.swap(0, 4);
                    return;
                }

            }
            // Move left
            else
            {
                if(safe[0])
                {
                    ew.swap(0, 1);
                    return;
                }
            }
        }
        // Move up/down
        else if(isLarge)
        {
            --stabCount;
            stable = false;
            // Move Down
            if(udDisp > 0)
            {
                if(safe[2])
                {
                    ew.swap(0, 3);
                    return;
                }
            }
            // Move Up
            else
            {
                if(safe[1])
                {
                    ew.swap(0, 2);
                    return;
                }
            }
        }
        // Count stable cycles
        else
        {
            if(stabCount > stableMinCycle)
            {
                stable = true;
            }
            else
            {
                ++stabCount;
                stable = false;
            }
        }
    }
}

/**
 * Nucleus ID variation enabler
 * \color #2f2
 * \symbol Nu
 * \symmetries normal
 *
 * \author R. Stephens Summerlin
 * \license lgpl
 */
element NucleusB
{
    // Utilities
    EventWindow ew;

    /**
     * Primary runtime logic
     */
    Void behave()
    {
        Nucleus n;
        n.nId = 2;
        ew[0] = n;
    }
}

/**
 * Nucleus ID variation enabler
 * \color #ff2
 * \symbol Nu
 * \symmetries normal
 *
 * \author R. Stephens Summerlin
 * \license lgpl
 */
element NucleusC
{
    // Utilities
    EventWindow ew;

    /**
     * Primary runtime logic
     */
    Void behave()
    {
        Nucleus n;
        n.nId = 3;
        ew[0] = n;
    }
}
