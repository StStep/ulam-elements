/**
 * Cytoplasm is a size management element
 * \color #22f
 * \symbol Cy
 * \symmetries normal
 *
 * \author R. Stephens Summerlin
 *
 * \license lgpl
 *
 */

element Cytoplasm
{
    // Types
    typedef Nucleus.ID ID;
    typedef Nucleus.Dist Dist;
    typedef EventWindow.SiteNum SiteNum;

    // Utilities
    EventWindow ew;

    // Model Parameters
    /** The weak thresh, no spreading */
    parameter Dist weakLim = 3;
    /** The dist thresh where cyto dies, 0 disables */
    parameter Dist deadLim = 1;

    // Data members
    ID id;
    Dist dist;

    Void harden()
    {
        Membrane m;
        ew[0] = m;
    }

    Void die()
    {
        Empty e;
        ew[0] = e;
    }

    Void spread(SiteNum site)
    {
        Cytoplasm c;
        c.id = id;
        c.dist = (Dist) (dist - 1);
        ew[site] = c;
    }

    /**
     * Primary runtime logic
     */
    Void behave()
    {
        // Setup temp map
        Dist max = dist;

        // Loop through neighbors
        for(SiteNum site = 1; site <= 4; ++site)
        {
            Atom a = ew[site];

            // If friendly Cyto neigbor, calc max dist, otherwise membrane
            if(a is Cytoplasm)
            {
                Cytoplasm neig = (Cytoplasm) a;
                if(neig.id != id)
                {
                    harden();
                }
                else
                {
                    if(neig.dist > max)
                    {
                        max = neig.dist;
                    }
                }
            }
            // If empty and not on distance edge, make cyto
            else if(a is Empty)
            {
                if(dist > weakLim)
                {
                    spread(site);
                }
            }
            // If membrane, do nothing
            else if(a is Membrane)
            {
                // Do nothing
            }
            else if(a is Nucleus)
            {
                Nucleus neig = (Nucleus) a;
                if(neig.id != id)
                {
                    harden();
                }
            }
            // Else become membrane if unknown
            else
            {
                harden();
            }
        }

        // Decay and set
        --max;

        // Die when dist less than death lim
        if(max < deadLim)
        {
            die();
        }
        else
        {
            dist = max;
        }

    }
}
